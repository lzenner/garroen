////////////////////////////////////
// Adds Garrick and Imoen romance //
////////////////////////////////////

DEFINE_ACTION_FUNCTION garroen BEGIN
	// Grab StrRef for Garrick to use in map note
	OUTER_SET STRREF_GARRICK = RESOLVE_STR_REF (~Garrick~)

	// Other variables that will be set depending upon game installed
	OUTER_SPRINT MOVE_GARRICK_TOK_BG1 ~~
	OUTER_SPRINT MOVE_GARRICK_TOK_SOD ~~
	OUTER_SPRINT TRANSITIONS_IMOEN_TRAIN ~~
	OUTER_SPRINT DIALOGUE_BDSCHAEL_EET ~~
	OUTER_SPRINT TRIGGER_RESTING_INN ~~
	OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_1 ~~
	OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_2 ~~
	OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_3 ~~
	OUTER_SPRINT BEGIN_GARRICK_BANTER ~~
	OUTER_SPRINT BEGIN_IMOEN_BANTER ~~
	OUTER_SET GARRICK_SAY_YOURE_BACK = 15778
	OUTER_SET GARRICK_REPLY_WELCOME = 15781
	OUTER_SET GARRICK_REPLY_SORRY = 15782
	OUTER_SET IMOEN_SAY_YOURE_BACK = 16412
	OUTER_SET IMOEN_REPLY_WELCOME = 16413
	OUTER_SET IMOEN_REPLY_SORRY = 16414

	ACTION_IF GAME_IS ~eet~ BEGIN
		// New area for the wedding
		COPY ~%mod_root%/objects/#LBG0132.are~ ~override~
		
		OUTER_SET GARRICK_SAY_YOURE_BACK = 215778
		OUTER_SET GARRICK_REPLY_WELCOME = 215781
		OUTER_SET GARRICK_REPLY_SORRY = 215782
		OUTER_SET IMOEN_SAY_YOURE_BACK = 216412
		OUTER_SET IMOEN_REPLY_WELCOME = 216413
		OUTER_SET IMOEN_REPLY_SORRY = 216414
		OUTER_SPRINT DIALOGUE_BDSCHAEL_EET ~~~~~REPLACE_SAY BDSCHAEL 277 @2143 // ~The bard Garrick, the witch Dynaheir and her bodyguard Minsc are staying at the Three Old Kegs...~
			REPLACE_SAY BDSCHAEL 279 @2144 // ~You tell me. We are to acquire equipment and allies for the trip north. There's a bard and a couple of Rashemi adventurers we can talk to at the Three Old Kegs, the thieves at the Elfsong Tavern. Or we could just return to the palace.~
			REPLACE_SAY BDSCHAEL 280 @2145 // ~You tell me. We are to acquire equipment and allies for the trip north. There's the thieves at the Elfsong Tavern, or a monk at the Iron Throne Building. Or we could just return to the palace.~
			ALTER_TRANS BDSCHAEL
				BEGIN 280 END
				BEGIN 3 END
				BEGIN
					"TRIGGER" ~False()~
				END
			REPLACE_SAY BDSCHAEL 281 @2146 // ~You tell me. We are to acquire equipment and allies for the trip north. There's the thieves at the Elfsong Tavern. Or we could just return to the palace.~
			ALTER_TRANS BDSCHAEL
				BEGIN 281 END
				BEGIN 2 END
				BEGIN
					"TRIGGER" ~False()~
				END~~~~~
	END ELSE ACTION_IF GAME_IS ~bgee~ BEGIN
		// New area for the wedding
		COPY ~%mod_root%/objects/#LAR0132.are~ ~override\#LBG0132.are~
	END
	
	// Limit some bg1npc banters depending upon progress of romance
	ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ 0) BEGIN
		OUTER_SET GARRICK_BANTER_1 = STATE_WHICH_SAYS 224 IN ~bg1npc\tra\%s\x#garri.tra~ FROM ~%IMOEN_BANTER%~
		OUTER_SET GARRICK_BANTER_2 = STATE_WHICH_SAYS 244 IN ~bg1npc\tra\%s\x#garri.tra~ FROM ~%GARRICK_BANTER%~
		OUTER_SET GARRICK_BANTER_3 = STATE_WHICH_SAYS 165 IN ~bg1npc\tra\%s\x#garri.tra~ FROM ~%GARRICK_BANTER%~
		ACTION_IF GARRICK_BANTER_1 > ~-1~ BEGIN
			OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_1 ~REPLACE_SAY %IMOEN_BANTER% %GARRICK_BANTER_1% @2660~
		END
		ACTION_IF GARRICK_BANTER_2 > ~-1~ BEGIN
			OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_2 ~~~~~ADD_STATE_TRIGGER %GARRICK_BANTER% %GARRICK_BANTER_2% ~Global("#L_GIRomance","GLOBAL",0)~ ~~~~~
		END
		ACTION_IF GARRICK_BANTER_3 > ~-1~ BEGIN
			// This one is of him being mean to Khalid - I won't have it
			OUTER_SPRINT DIALOGUE_BG1NPC_BANTER_3 ~~~~~ADD_STATE_TRIGGER %GARRICK_BANTER% %GARRICK_BANTER_3% ~False()~ ~~~~~
		END
	END
	
	// Make sure banter files exist for Imoen and GARRICK
	ACTION_IF !FILE_EXISTS_IN_GAME ~%GARRICK_BANTER%.dlg~ BEGIN
		OUTER_SPRINT BEGIN_GARRICK_BANTER ~BEGIN %GARRICK_BANTER% END~
	END
	ACTION_IF !FILE_EXISTS_IN_GAME ~%IMOEN_BANTER%.dlg~ BEGIN
		OUTER_SPRINT BEGIN_IMOEN_BANTER ~BEGIN %IMOEN_BANTER% END~
	END
	
	// Make sure they're set up for doing banters
	COPY_EXISTING ~interdia.2da~ ~override~
		PATCH_IF (GAME_IS ~bg2ee~) BEGIN
			READ_2DA_ENTRIES_NOW curList 3
		END ELSE BEGIN
			READ_2DA_ENTRIES_NOW curList 2
		END
		SET garrickSet = 0
		SET imoenSet = 0
		SET newRow = 1

		FOR (iRow = 0; iRow < curList; ++iRow) BEGIN
			READ_2DA_ENTRY_FORMER curList iRow 0 npcDV
			PATCH_MATCH ~%npcDV%~ WITH 
				~%IMOEN_DV%~ BEGIN SET imoenSet = 1 END 
				~Garrick~ BEGIN SET garrickSet = 1 END
				DEFAULT // do nothing
			END
			newRow = iRow + 1
		END

		PATCH_IF (imoenSet = 0) THEN BEGIN
			PATCH_IF (GAME_IS ~bg2ee~) THEN BEGIN
				INSERT_2DA_ROW newRow 3 ~%IMOEN_DV% %IMOEN_BANTER% BIMOEN25~
			END ELSE BEGIN
				INSERT_2DA_ROW newRow 2 ~%IMOEN_DV% %IMOEN_BANTER%~
			END
			newRow = newRow + 1
		END
		PATCH_IF (garrickSet = 0) THEN BEGIN
			PATCH_IF (GAME_IS ~bg2ee~) THEN BEGIN
				INSERT_2DA_ROW newRow 3 ~Garrick %GARRICK_BANTER% BGARRI25~
			END ELSE BEGIN
				INSERT_2DA_ROW newRow 2 ~Garrick %GARRICK_BANTER%~
			END
		END
	BUT_ONLY
	
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT TRIGGER_RESTING_INN ~OR(20)
			AreaCheck("%Nashkel_Inn%")
			AreaCheck("%Beregost_RedSheaf_L1%")
			AreaCheck("%Beregost_FeldepostsInn_L1%")
			AreaCheck("%Beregost_BurningWizard_L1%")
			AreaCheck("%Beregost_JovialJuggler_L1%")
			AreaCheck("%Candlekeep_Inn_L1%")
			AreaCheck("%FriendlyArmInn_L1%")
			AreaCheck("%SEBaldursGate_Inn_L1%")
			AreaCheck("%BaldursGateDocks_JopalinsTavern%")
			AreaCheck("%EBaldursGate_Inn_L1%")
			AreaCheck("%CentralBaldursGate_DrakonTavern%")
			AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
			AreaCheck("%NWBaldursGate_Tavern_L1%")
			AreaCheck("%EBaldursGate_KeexieTavern_L1%")
			AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
			AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
			AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
			AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
			AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
			NextTriggerObject("FF_Camp")
			IsOverMe(Player1)~
	END ELSE BEGIN
		OUTER_SPRINT TRIGGER_RESTING_INN ~OR(19)
			AreaCheck("%Nashkel_Inn%")
			AreaCheck("%Beregost_RedSheaf_L1%")
			AreaCheck("%Beregost_FeldepostsInn_L1%")
			AreaCheck("%Beregost_BurningWizard_L1%")
			AreaCheck("%Beregost_JovialJuggler_L1%")
			AreaCheck("%Candlekeep_Inn_L1%")
			AreaCheck("%FriendlyArmInn_L1%")
			AreaCheck("%SEBaldursGate_Inn_L1%")
			AreaCheck("%BaldursGateDocks_JopalinsTavern%")
			AreaCheck("%EBaldursGate_Inn_L1%")
			AreaCheck("%CentralBaldursGate_DrakonTavern%")
			AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
			AreaCheck("%NWBaldursGate_Tavern_L1%")
			AreaCheck("%EBaldursGate_KeexieTavern_L1%")
			AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
			AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
			AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
			AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
			AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")~
	END
	// Move Garrick and Imoen to Three Old Kegs at the start of Chapter 5 (BG1)
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		OUTER_SPRINT MOVE_GARRICK_TOK_BG1 ~~~~~IF
	Global("Chapter","GLOBAL",5)
	Global("#L_GIAwayForWedding","GLOBAL",0)
	!InPartyAllowDead("GARRICK")
	!TriggerOverride("GARRICK",InMyArea(Player1))
	Global("#L_MovedGarrickTOK_BG1","GLOBAL",0)
	Global("#L_GarrickTOK_BG1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_MovedGarrickTOK_BG1","GLOBAL",1)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",1)
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","GARRICK",[360.140])
		Continue()
END

IF
	Global("Chapter","GLOBAL",5)
	Global("#L_GIAwayForWedding","GLOBAL",0)
	!InPartyAllowDead("%IMOEN_DV%")
	!TriggerOverride("%IMOEN_DV%",InMyArea(Player1))
	Global("#L_MovedImoenTOK_BG1","GLOBAL",0)
	Global("#L_ImoenTOK_BG1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_MovedImoenTOK_BG1","GLOBAL",1)
		SetGlobal("#L_ImoenFeldepost","GLOBAL",0)
		SetGlobal("#L_ImoenTOK_BG1","GLOBAL",1)
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","%IMOEN_DV%",[365.215])
		Continue()
END

// Return them to their proper inn after their honeymoon
IF
	GlobalLT("Chapter","GLOBAL",5)
	Global("#L_GIMarried","GLOBAL",0)
	GlobalGT("#L_GIAwayForWedding","GLOBAL",0)
	GlobalTimerExpired("#L_GIAwayForWedding","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("#L_GIMarried","GLOBAL",1)
		SetGlobal("#L_GIAwayForWedding","GLOBAL",0)
		SetGlobal("#L_GarrickPositioned","%Beregost_FeldepostsInn_L1%",0)	// So the room will redo it's positioning routine
		SetGlobal("#L_ImoenPositioned","%Beregost_FeldepostsInn_L1%",0)
		MoveGlobal("%Beregost_FeldepostsInn_L1%","GARRICK",[900.510])
		MoveGlobal("%Beregost_FeldepostsInn_L1%","%IMOEN_DV%",[780.580])
		Continue()
END

IF
	GlobalGT("Chapter","GLOBAL",4)
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_GIMarried","GLOBAL",0)
	GlobalGT("#L_GIAwayForWedding","GLOBAL",0)
	GlobalTimerExpired("#L_GIAwayForWedding","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("#L_GIMarried","GLOBAL",1)
		SetGlobal("#L_GIAwayForWedding","GLOBAL",0)
		SetGlobal("#L_GarrickPositioned","%NBaldursGate_ThreeOldKegs_L1%",0)	// So the room will redo it's positioning routine
		SetGlobal("#L_ImoenPositioned","%NBaldursGate_ThreeOldKegs_L1%",0)
		SetGlobal("#L_MovedGarrickTOK_BG1","GLOBAL",1)							// In case chapter 5 started during their honeymoon
		SetGlobal("#L_MovedImoenTOK_BG1","GLOBAL",1)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_ImoenFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",1)
		SetGlobal("#L_ImoenTOK_BG1","GLOBAL",1)
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","GARRICK",[360.140])
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","%IMOEN_DV%",[365.215])
		Continue()
END~~~~~
	END  // end of game_is bgee eet

	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT MOVE_GARRICK_TOK_SOD ~~~~~IF
	!InPartyAllowDead("GARRICK")
	Global("EndOfBG1","GLOBAL",1)
	!TriggerOverride("GARRICK",InMyArea(Player1))
	Global("#L_MovedGarrickTOK_SoD","GLOBAL",0)
	Global("#L_GarrickTOK_SoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_MovedGarrickTOK_SoD","GLOBAL",1)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_SoD","GLOBAL",1)
		SetGlobal("#L_GarrickModded","GLOBAL",1)
		SetGlobal("#L_GarrickOKInBG1Areas","GLOBAL",1)
		ActionOverride("GARRICK",SetGlobal("BD_Joined","LOCALS",0))
	    ActionOverride("GARRICK",SetGlobal("BD_Retreat","locals",0))
		IncrementGlobal("#L_GIRomance","GLOBAL",14)
	    ApplySpellRES("bdrejuve","GARRICK")
	    ChangeEnemyAlly("GARRICK",NEUTRAL)
    	ChangeSpecifics("GARRICK",ALLIES)
		MoveGlobal("BD0106","GARRICK",[360.140])
		Continue()
END~~~~~
	END  // end of game includes sod

	// Globals, timers and moves them to Three Old Kegs
	EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/baldur.baf~ EVAL

	// Kicks off scenes that happen just before rest
	LAF UPDATE_BCS
		INT_VAR
			action_taken = 2		// 0 = APPEND_FILE TEXT, 1 = EXTEND_TOP, 2 = EXTEND_BOTTOM
		STR_VAR
			baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
			baf_name = "player1d"			// like my_new_BG0146
			bcs_name = "player1d"			// like BG0146
			using_tra = EVAL ~%tra_loc%/%s/dialogues.tra~
	END
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGICamp.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIDngn.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIInn.baf~ USING ~%tra_loc%/%s/dialogues.tra~

	// Globals and kicks off scenes in the inns
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		EXTEND_BOTTOM ~%Beregost_FeldepostsInn_L1%.BCS~ ~%mod_root%/scripts/feldeposts_inn_bg1.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~ 
		EXTEND_BOTTOM ~%NBaldursGate_ThreeOldKegs_L1%.BCS~ ~%mod_root%/scripts/three_old_kegs_bg1.baf~ EVAL
	END
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		EXTEND_BOTTOM ~BD0106.BCS~ ~%mod_root%/scripts/three_old_kegs_sod.baf~ EVAL
	END

	// Campaign independent scenes
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIIdle.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISing.baf~ USING ~%tra_loc%/%s/dialogues.tra~	
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISigh.baf~ USING ~%tra_loc%/%s/dialogues.tra~	
	
	// Won't fire unless Transitions is installed but is referenced in #LGISing
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGITOK.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	
	// Garrick's stores
	COPY ~%mod_root%/objects/#LGISto1.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto2.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto3.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto4.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto5.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto6.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto7.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto8.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto9.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
		
	// Their 'kiss and make it better' +1 HP spell
	// Made it rather powerful - cures things that distract them from each other
	// like charm, sleep, confusion, etc
	COPY ~%mod_root%/objects/#LGIKiss.spl~ ~override~
		SAY (0x0008) @1001 // Kiss

	// Get rid of his infatuation with Skie
	COPY_EXISTING_REGEXP GLOB ~GARRIC.*.CRE~ ~override~	
		WRITE_LONG (0x0140) ~-1~
	
	ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ (ID_OF_LABEL "transitions.tp2" "#L-TRANSITIONS-MAIN")) BEGIN
		// Get a couple strrefs from Transitions 
		// so we can recreate portion of the dialogue I needed to sidestep
		// Luckily it should be installed first
		OUTER_SET TRANS_GO_TRAIN = STATE_WHICH_SAYS 2121 IN ~weidu_external/workspace/transitions/languages/%s/dialogues.tra~ FROM ~%IMOEN_POST%~
		ACTION_IF TRANS_GO_TRAIN > ~-1~ BEGIN
			WITH_TRA ~weidu_external/workspace/transitions/languages/%s/dialogues.tra~ BEGIN
				OUTER_SET TRANS_WONT_NEED_YOU = RESOLVE_STR_REF (@2322)
			END
			OUTER_SPRINT TRANSITIONS_IMOEN_TRAIN ~~~~~IF ~GlobalGT("#L_ImTrainRsp","GLOBAL",0)~ THEN REPLY #%TRANS_WONT_NEED_YOU% GOTO %TRANS_GO_TRAIN%~~~~~
		END
	END

	// Compile their BG1 dialogues and scripts
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		// Advance romance days when days are skipped in the game
		COPY_EXISTING ~SHIP.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~StartCutSceneEx("ISLOFF",FALSE)~ ~StartCutSceneEx("ISLOFF",TRUE)~
			END
		EXTEND_TOP ~ISLOFF.BCS~ ~%mod_root%/scripts/isloff.baf~ EVAL
		EXTEND_TOP ~ISLON.BCS~ ~%mod_root%/scripts/islon.baf~ EVAL

		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_bg1.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_all.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_taverns.d~ USING ~%tra_loc%/%s/dialogues.tra~  // Used for BG1 and BG2
		EXTEND_TOP ~%GARRICK_BCS%.bcs~ ~%mod_root%/scripts/garrick_.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~ 
		EXTEND_TOP ~%IMOEN_BCS%.bcs~ ~%mod_root%/scripts/imoen_.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~ 
		
		// Add areas where they comment about picnics or wanting to camp
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISpot.baf~
		COPY_EXISTING ~%FireLeafForest%.ARE~ ~override~
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 0		//Proximity Trigger
					ab_RT_BbLX = 3350
					ab_RT_BbLY = 1475
					ab_RT_BbHX = 4225
					ab_RT_BbHY = 2000
					ab_RT_VxPr = 4
					ab_RT_Fbit = 2		// Resets
					ab_RT_TDtD = 100
					ab_RT_TRmD = 100
					ab_RT_TSet = 1
					ab_RT_Vx_X_0 = 3375
					ab_RT_Vx_Y_0 = 1475
					ab_RT_Vx_X_1 = 3350
					ab_RT_Vx_Y_1 = 1900
					ab_RT_Vx_X_2 = 4025
					ab_RT_Vx_Y_2 = 2000
					ab_RT_Vx_X_3 = 4225
					ab_RT_Vx_Y_3 = 1750
				STR_VAR
					ab_RT_Rbcs = ~#LGISpot~
					ab_RT_Name = "#LGISpot"
			END
		COPY_EXISTING ~%DryadFalls%.ARE~ ~override~
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 0		//Proximity Trigger
					ab_RT_BbLX = 2975
					ab_RT_BbLY = 1750
					ab_RT_BbHX = 3370
					ab_RT_BbHY = 2250
					ab_RT_VxPr = 4
					ab_RT_Fbit = 2		// Resets
					ab_RT_TDtD = 100
					ab_RT_TRmD = 100
					ab_RT_TSet = 1
					ab_RT_Vx_X_0 = 2975
					ab_RT_Vx_Y_0 = 1750
					ab_RT_Vx_X_1 = 3350
					ab_RT_Vx_Y_1 = 1775
					ab_RT_Vx_X_2 = 3370
					ab_RT_Vx_Y_2 = 2250
					ab_RT_Vx_X_3 = 3015
					ab_RT_Vx_Y_3 = 2220
				STR_VAR
					ab_RT_Rbcs = ~#LGISpot~
					ab_RT_Name = "#LGISpot"
			END
			
		// Have folks in Feldepost bemoan Garrick's leaving
		// Give the handles...
		COPY_EXISTING ~MTOWBEST.CRE~ ~override~
			WRITE_ASCII 0x280 ~Patron_1~ #32 // set DV
		COPY_EXISTING ~FTOWBESC.CRE~ ~override~
			WRITE_ASCII 0x280 ~Patron_2~ #32 // set DV
		COPY_EXISTING ~Bart5.CRE~ ~override~
			WRITE_ASCII 0x280 ~Bartender~ #32 // set DV
			
		// Make it its own master area
//		COPY_EXISTING ~MASTAREA.2DA~ ~override~
//			INSERT_2DA_ROW 2 2 ~#LBG0132 Value~
		
		// A couple wedding guests with no handles want to wander
		COPY_EXISTING ~Parda.CRE~ ~override~
			WRITE_ASCII 0x280 ~Parda~ #32 // set DV
		COPY_EXISTING ~Hephis.CRE~ ~override~
			WRITE_ASCII 0x280 ~Hephis~ #32 // set DV
		COPY_EXISTING ~Gyllia.CRE~ ~override~
			WRITE_ASCII 0x280 ~Gyllia~ #32 // set DV
		COPY_EXISTING ~Bjorni.CRE~ ~override~
			WRITE_ASCII 0x280 ~Bjornin~ #32 // set DV
			
		// Wedding participants and guests
		COPY ~%mod_root%/objects/#LBride.cre~ ~override~
		COPY ~%mod_root%/objects/#LGroom.cre~ ~override~
		COPY_EXISTING ~AGNASI.CRE~ ~override~
			WRITE_ASCII 0x280 ~PriestF~ #32 // set DV
		COPY_EXISTING ~CHANTH.CRE~ ~override~
			WRITE_ASCII 0x280 ~PriestM~ #32 // set DV
			
		// Empty Script file to override guest actor scripts
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LBlank.baf~ 
		
		// Wedding cut scene
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIWeda.baf~ USING ~%tra_loc%/%s/dialogues.tra~  
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIWedb.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	END // End of GAME_IS ~bgee eet~

	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		// Set globals, timers, etc
		EXTEND_TOP ~BDBALDUR.BCS~ ~%mod_root%/scripts/bdbaldur.baf~ EVAL

		// Get rid of cameo Garrick in Elfsong Tavern
		COPY_EXISTING ~BD0109.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
        		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride("BDGARRIC",DialogueInterrupt(FALSE))~ ~ActionOverride("BDGARRIC",DestroySelf()) RemoveMapNote([806.325],%STRREF_GARRICK%)~
			END

		// Advance romance days when days are skipped in the game
		COPY_EXISTING ~BD0103.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(FOURTEEN_DAYS)~ ~AdvanceTime(FOURTEEN_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",13)~
			END
		COPY_EXISTING ~BDCUT10.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(THREE_DAYS)~ ~AdvanceTime(THREE_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",2)~
			END
		COPY_EXISTING ~BDSODTRN.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(TEN_DAYS)~ ~AdvanceTime(TEN_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",9)~
			END
		
		// Start scenes in palace (3rd floor)
		EXTEND_TOP ~BD0103.BCS~ ~%mod_root%/scripts/palace_3rd_floor_sod.baf~ USING ~%tra_loc%/%s/dialogues.tra~

		OUTER_SPRINT JOIN_WITH_IMOEN ~~
		ACTION_IF FILE_EXISTS ~override/c#imback.bcs~ BEGIN
			// Move Garrick to first camp along with Imoen
			COPY_EXISTING ~c#imback.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1268.3238]))~ ~ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1268.3238])) ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1310.3230]))~
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~MoveGlobal("bd1000","IMOEN2",[1268.3238])~ ~MoveGlobal("bd1000","IMOEN2",[1268.3238]) MoveGlobal("BD1000","GARRICK",[1310.3230]) ActionOverride("GARRICK",ChangeAIScript("#LGar15O",OVERRIDE)) ActionOverride("GARRICK",ChangeAIScript("BDGARRCC",CLASS)) ActionOverride("GARRICK",ChangeAIScript("",RACE)) ActionOverride("GARRICK",ChangeAIScript("",GENERAL)) ActionOverride("GARRICK",ChangeAIScript("",DEFAULT)) ActionOverride("GARRICK",SetDialogue("BDGARRIC"))~
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~FaceObject(Player1)~ ~FaceObject(Player1) ActionOverride("GARRICK",FaceObject("IMOEN2")) SetGlobal("#L_GarrickMoved","BD1000",1) SetGlobal("#L_FollowingImoen","BD1000",1) ActionOverride("GARRICK",MoveToObjectFollow("IMOEN2"))~
				END
			// Have Garrick join the group when Imoen does
			OUTER_SPRINT JOIN_WITH_IMOEN ~~~~~ALTER_TRANS BDIMOENS
	BEGIN 7 END
	BEGIN 0 END
	BEGIN
		"ACTION" ~SetGlobal("C#IM_ImoenRejoinsbd1000","GLOBAL",5) SetGlobal("#L_GarrickJoined","GLOBAL",1) SetGlobal("#L_FollowingImoen","MYAREA",0) ActionOverride("GARRICK",JoinParty()) ActionOverride("GARRICK",SetDialogue("BDGARRIJ")) SetDialogue("BDIMOEN") JoinParty()~
	END~~~~~
		END ELSE BEGIN
			// Move Garrick to the first camp based on I4E Globals
			// in case I4E is installed later
			EXTEND_TOP ~BD1000.BCS~ ~%mod_root%/scripts/first_camp_sod.baf~
		END

		// Garrick's new override script
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGar15O.baf~

		// Compile the dialogues
		OUTER_SPRINT GARRICK_JOINED ~BDGARRIJ~
		OUTER_SPRINT IMOEN_JOINED ~BDIMOEN~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_sod.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_all.d~ USING ~%tra_loc%/%s/dialogues.tra~

		// Give Rayphus nice things to say to Garrick
		// when they're in camp together
		// The file may be empty...
		LAF UPDATE_BCS
			STR_VAR
				baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
				baf_name = "bdrayphu"			// like my_new_BG0146
				bcs_name = "BDRAYPHU"			// like BG0146
				using_tra = EVAL ~%tra_loc%/%s/dialogues.tra~
		END
		
		// Don't allow camping inside the FF camp or else there's no way to determine if they're using Thaird or not
		EXTEND_TOP ~BD1000.BCS~ ~%mod_root%/scripts/bd1000.baf~

	END // end of game is sod
END
