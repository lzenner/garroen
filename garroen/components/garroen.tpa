////////////////////////////////////
// Adds Garrick and Imoen romance //
////////////////////////////////////

DEFINE_ACTION_FUNCTION garroen BEGIN
	// Grab StrRef for Garrick to use in map note
	OUTER_SET STRREF_GARRICK = RESOLVE_STR_REF (~Garrick~)

	// Other variables that will be set depending upon game installed
	OUTER_SPRINT MOVE_GARRICK_TOK_BG1 ~~
	OUTER_SPRINT MOVE_GARRICK_TOK_SOD ~~
	OUTER_SPRINT TRANSITIONS_IMOEN_TRAIN ~~

	// Move Garrick to Three Old Kegs when Imoen goes into training
	// Which version(s) of the place differ depending upon installed game
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		OUTER_SPRINT MOVE_GARRICK_TOK_BG1 ~~~~~IF
	Global("#L_ImoenInPalace","GLOBAL",1)
	!InPartyAllowDead("GARRICK")
	!TriggerOverride("GARRICK",InMyArea(Player1))
	Global("EndOfBG1","GLOBAL",0)
	Global("#L_MovedGarrickTOK_BG1","GLOBAL",0)
	Global("#L_GarrickTOK_BG1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_MovedGarrickTOK_BG1","GLOBAL",1)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",1)
		SetGlobal("#L_GarrickModded","GLOBAL",1)
		SetGlobal("#L_GarrickOKInBG1Areas","GLOBAL",1)
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","GARRICK",[360.140])
		Continue()
END

IF
	Global("#L_GIMarried","GLOBAL",0)
	Global("#L_ImoenInPalace","GLOBAL",1)
	Global("EndOfBG1","GLOBAL",0)
	GlobalTimerExpired("#L_GIAwayForWedding","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("#L_GIMarried","GLOBAL",1)
		SetGlobal("#L_GarrickPositioned","%NBaldursGate_ThreeOldKegs_L1%",0)
		SetGlobal("#L_ImoenSet","%NBaldursGate_ThreeOldKegs_L1%",0)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_ImoenFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",1)
		MoveGlobal("%NBaldursGate_ThreeOldKegs_L1%","GARRICK",[360.140])
		Continue()
END~~~~~
	END  // end of game_is bgee eet

	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		OUTER_SPRINT MOVE_GARRICK_TOK_SOD ~~~~~IF
	!InPartyAllowDead("GARRICK")
	Global("EndOfBG1","GLOBAL",1)
	!TriggerOverride("GARRICK",InMyArea(Player1))
	Global("#L_MovedGarrickTOK_SoD","GLOBAL",0)
	Global("#L_GarrickTOK_SoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_MovedGarrickTOK_SoD","GLOBAL",1)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_SoD","GLOBAL",1)
		SetGlobal("#L_GarrickModded","GLOBAL",1)
		SetGlobal("#L_GarrickOKInBG1Areas","GLOBAL",1)
		ActionOverride("GARRICK",SetGlobal("BD_Joined","LOCALS",0))
	    ActionOverride("GARRICK",SetGlobal("BD_Retreat","locals",0))
		IncrementGlobal("#L_GIRomance","GLOBAL",14)
	    ApplySpellRES("bdrejuve","GARRICK")
	    ChangeEnemyAlly("GARRICK",NEUTRAL)
    	ChangeSpecifics("GARRICK",ALLIES)
		MoveGlobal("BD0106","GARRICK",[360.140])
		Continue()
END~~~~~
	END  // end of game includes sod

	// Globals, timers, kicks off scenes when idle, and moves Garrick to Three Old Kegs
	EXTEND_TOP ~BALDUR.BCS~ ~%mod_root%/scripts/baldur.baf~ EVAL

	// Kicks off scenes that happen just before rest
	EXTEND_BOTTOM ~PLAYER1D.BCS~ ~%mod_root%/scripts/player1d.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~

	// Globals and kicks off scenes in the inns
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		EXTEND_BOTTOM ~%Beregost_FeldepostsInn_L1%.BCS~ ~%mod_root%/scripts/feldeposts_inn_bg1.baf~ EVAL
		EXTEND_BOTTOM ~%NBaldursGate_ThreeOldKegs_L1%.BCS~ ~%mod_root%/scripts/three_old_kegs_bg1.baf~ EVAL
	END
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		EXTEND_BOTTOM ~BD0106.BCS~ ~%mod_root%/scripts/three_old_kegs_sod.baf~ EVAL
	END

	// The scenes
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIIdle.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISing.baf~ USING ~%tra_loc%/%s/dialogues.tra~	
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISigh.baf~ USING ~%tra_loc%/%s/dialogues.tra~	
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGICamp.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIDngn.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGIInn.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	
	// Garrick's stores
	COPY ~%mod_root%/objects/#LGISto1.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto2.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto3.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto4.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto5.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto6.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto7.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto8.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
	COPY ~%mod_root%/objects/#LGISto9.sto~ ~override~
		SAY (0x016c + 0x0008) @1000	// Round of drinks
		
	// Their 'kiss and make it better' +1 HP spell
	COPY ~%mod_root%/objects/#LGIKiss.spl~ ~override~
		SAY (0x0008) @1001 // Kiss

	ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ (ID_OF_LABEL "transitions.tp2" "#L-TRANSITIONS-MAIN")) BEGIN
		// Get a couple strrefs from Transitions 
		// so we can recreate portion of the dialogue I needed to sidestep
		// Luckily it should be installed first
		OUTER_SET TRANS_GO_TRAIN = STATE_WHICH_SAYS 2121 IN ~weidu_external/workspace/transitions/languages/%s/dialogues.tra~ FROM ~%IMOEN_POST%~
		ACTION_IF TRANS_GO_TRAIN > ~-1~ BEGIN
			WITH_TRA ~weidu_external/workspace/transitions/languages/%s/dialogues.tra~ BEGIN
				OUTER_SET TRANS_WONT_NEED_YOU = RESOLVE_STR_REF (@2322)
			END
			OUTER_SPRINT TRANSITIONS_IMOEN_TRAIN ~~~~~IF ~GlobalGT("#L_ImTrainRsp","GLOBAL",0)~ THEN REPLY #%TRANS_WONT_NEED_YOU% GOTO %TRANS_GO_TRAIN%~~~~~
		END

		// Compile scenes that only occur for Transitions
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGITOK.baf~ USING ~%tra_loc%/%s/dialogues.tra~
	END

	// Compile their BG1 dialogues and override scripts
	ACTION_IF GAME_IS ~bgee eet~ BEGIN
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_bg1.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_all.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_taverns.d~ USING ~%tra_loc%/%s/dialogues.tra~  // Used for BG1 and BG2
		EXTEND_TOP ~garrick_.bcs~ ~%mod_root%/scripts/garrick_.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~ 
		EXTEND_TOP ~imoen_.bcs~ ~%mod_root%/scripts/imoen_.baf~ EVAL USING ~%tra_loc%/%s/dialogues.tra~ 
		
		// Add areas where they comment about picnics or wanting to camp
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGISpot.baf~
		COPY_EXISTING ~%FireLeafForest%.ARE~ ~override~
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 0		//Proximity Trigger
					ab_RT_BbLX = 3350
					ab_RT_BbLY = 1475
					ab_RT_BbHX = 4225
					ab_RT_BbHY = 2000
					ab_RT_VxPr = 4
					ab_RT_Fbit = 2		// Resets
					ab_RT_TDtD = 100
					ab_RT_TRmD = 100
					ab_RT_TSet = 1
					ab_RT_Vx_X_0 = 3375
					ab_RT_Vx_Y_0 = 1475
					ab_RT_Vx_X_1 = 3350
					ab_RT_Vx_Y_1 = 1900
					ab_RT_Vx_X_2 = 4025
					ab_RT_Vx_Y_2 = 2000
					ab_RT_Vx_X_3 = 4225
					ab_RT_Vx_Y_3 = 1750
				STR_VAR
					ab_RT_Rbcs = ~#LGISpot~
					ab_RT_Name = "#LGISpot"
			END
		COPY_EXISTING ~%DryadFalls%.ARE~ ~override~
			LPF ADD_AREA_REGION_TRIGGER
				INT_VAR
					ab_RT_Type = 0		//Proximity Trigger
					ab_RT_BbLX = 2975
					ab_RT_BbLY = 1750
					ab_RT_BbHX = 3370
					ab_RT_BbHY = 2250
					ab_RT_VxPr = 4
					ab_RT_Fbit = 2		// Resets
					ab_RT_TDtD = 100
					ab_RT_TRmD = 100
					ab_RT_TSet = 1
					ab_RT_Vx_X_0 = 2975
					ab_RT_Vx_Y_0 = 1750
					ab_RT_Vx_X_1 = 3350
					ab_RT_Vx_Y_1 = 1775
					ab_RT_Vx_X_2 = 3370
					ab_RT_Vx_Y_2 = 2250
					ab_RT_Vx_X_3 = 3015
					ab_RT_Vx_Y_3 = 2220
				STR_VAR
					ab_RT_Rbcs = ~#LGISpot~
					ab_RT_Name = "#LGISpot"
			END
	END

	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		// Set globals, timers, idle scenes etc
		EXTEND_TOP ~BDBALDUR.BCS~ ~%mod_root%/scripts/bdbaldur.baf~ EVAL

		// Get rid of cameo Garrick in Elfsong Tavern
		COPY_EXISTING ~BD0109.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
        		REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride("BDGARRIC",DialogueInterrupt(FALSE))~ ~ActionOverride("BDGARRIC",DestroySelf()) RemoveMapNote([806.325],%STRREF_GARRICK%)~
			END

		// Advance romance days when days are skipped in the game
		COPY_EXISTING ~BD0103.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(FOURTEEN_DAYS)~ ~AdvanceTime(FOURTEEN_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",13)~
			END
		COPY_EXISTING ~BDCUT10.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(THREE_DAYS)~ ~AdvanceTime(THREE_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",2)~
			END
		COPY_EXISTING ~BDSODTRN.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(TEN_DAYS)~ ~AdvanceTime(TEN_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",9)~
			END
		COPY_EXISTING ~ISLOFF.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(TWENTYTWO_DAYS)~ ~AdvanceTime(TWENTYTWO_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",21)~
			END
		COPY_EXISTING ~ISLON.BCS~ ~override~
			DECOMPILE_AND_PATCH BEGIN
				REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AdvanceTime(TWENTYFIVE_DAYS)~ ~AdvanceTime(TWENTYFIVE_DAYS) IncrementGlobal("#L_GIRomance","GLOBAL",24)~
			END
		
		// Start scenes in palace (3rd floor)
		EXTEND_TOP ~BD0103.BCS~ ~%mod_root%/scripts/palace_3rd_floor_sod.baf~ USING ~%tra_loc%/%s/dialogues.tra~

		OUTER_SPRINT JOIN_WITH_IMOEN ~~
		ACTION_IF FILE_EXISTS ~override/c#imback.bcs~ BEGIN
			// Move Garrick to first camp along with Imoen
			COPY_EXISTING ~c#imback.bcs~ ~override~
				DECOMPILE_AND_PATCH BEGIN
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1268.3238]))~ ~ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1268.3238])) ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1310.3230]))~
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~MoveGlobal("bd1000","IMOEN2",[1268.3238])~ ~MoveGlobal("bd1000","IMOEN2",[1268.3238]) MoveGlobal("BD1000","GARRICK",[1310.3230]) ActionOverride("GARRICK",ChangeAIScript("#LGar15O",OVERRIDE)) ActionOverride("GARRICK",ChangeAIScript("BDGARRCC",CLASS)) ActionOverride("GARRICK",ChangeAIScript("",RACE)) ActionOverride("GARRICK",ChangeAIScript("",GENERAL)) ActionOverride("GARRICK",ChangeAIScript("",DEFAULT)) ActionOverride("GARRICK",SetDialogue("BDGARRIC"))~
					REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~FaceObject(Player1)~ ~FaceObject(Player1) ActionOverride("GARRICK",FaceObject("IMOEN2")) SetGlobal("#L_GarrickMoved","BD1000",1) SetGlobal("#L_FollowingImoen","BD1000",1) ActionOverride("GARRICK",MoveToObjectFollow("IMOEN2"))~
				END
			// Have Garrick join the group when Imoen does
			OUTER_SPRINT JOIN_WITH_IMOEN ~~~~~ALTER_TRANS BDIMOENS
	BEGIN 7 END
	BEGIN 0 END
	BEGIN
		"ACTION" ~SetGlobal("C#IM_ImoenRejoinsbd1000","GLOBAL",5) SetGlobal("#L_GarrickJoined","GLOBAL",1) SetGlobal("#L_FollowingImoen","MYAREA",0) ActionOverride("GARRICK",JoinParty()) ActionOverride("GARRICK",SetDialogue("BDGARRIJ")) SetDialogue("BDIMOEN") JoinParty()~
	END~~~~~
		END ELSE BEGIN
			// Move Garrick to the first camp based on I4E Globals
			// in case I4E is installed later
			EXTEND_TOP ~BD1000.BCS~ ~%mod_root%/scripts/first_camp_sod.baf~
		END

		// Garrick's new override script
		COMPILE EVALUATE_BUFFER ~%mod_root%/scripts/#LGar15O.baf~

		// Compile the dialogues
		OUTER_SPRINT %GARRICK_JOINED% ~BDGARRIJ~
		OUTER_SPRINT %IMOEN_JOINED% ~BDIMOEN~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_sod.d~ USING ~%tra_loc%/%s/dialogues.tra~
		COMPILE EVALUATE_BUFFER ~%mod_root%/dialogues/dialogues_all.d~ USING ~%tra_loc%/%s/dialogues.tra~

		// Give Rayphus nice things to say to Garrick
		// when they're in camp together
		// The file may be empty...
		LAF UPDATE_BCS
			STR_VAR
				baf_location = EVAL ~%mod_root%/scripts/~		// like ~mymod/bafs/~
				baf_name = "bdrayphu"			// like my_new_BG0146
				bcs_name = "BDRAYPHU"			// like BG0146
				using_tra = EVAL ~%tra_loc%/%s/dialogues.tra~
		END
		
		// Don't allow camping inside the FF camp or else there's no way to determine if they're using Thaird or not
		EXTEND_TOP ~BD1000.BCS~ ~%mod_root%/scripts/bd1000.baf~

	END // end of game is sod
END
