////////////////////////////////
// Globals and Timers for SoD //
////////////////////////////////

// Set global letting other mods know this one is installed
IF
	Global("#L_GarroenSoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GarroenSoD","GLOBAL",1)
		Continue()
END

// If Transitions is loaded and Imoen in training
IF
	Global("#L_ImoenInPalace","GLOBAL",1)
	!InPartyAllowDead("GARRICK")
	Global("EndOfBG1","GLOBAL",1)
	Global("#L_GarrickTOK_SoD","GLOBAL",0)
THEN
	RESPONSE #100
		// Move Garrick to SoD version of Three Old Kegs so he can be close
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_BG1","GLOBAL",0)
		SetGlobal("#L_GarrickTOK_SoD","GLOBAL",1)
		MoveGlobal("BD0106","GARRICK",[350.140])
END

// Every in-game day the romance will advance 
// when the Imoen is in traning and Garrick is at Three Old Kegs
IF
	OR(2)
		Global("#L_GarrickTOK_BG1","GLOBAL",1)
		Global("#L_GarrickTOK_SoD","GLOBAL",1)
	Global("#L_ImoenInPalace","GLOBAL",1)
	OR(2)
		Global("#L_GIParkedDay","GLOBAL",0)
		GlobalTimerExpired("#L_GIParkedDay","GLOBAL")
THEN
	RESPONSE #100
		IncrementGlobal("#L_GIRomance","GLOBAL",1)
		SetGlobalTimer("#L_GIParkedDay","GLOBAL",ONE_DAY)
		Continue()
END


