////////////////////////////////////////////////////////////
// Kicks off Garrick's performances and Imoen's reactions //
////////////////////////////////////////////////////////////

// Failsafe for when other mods mess with their parking spots
IF
	Global("#L_GarrickFeldepost","GLOBAL",1)
	Global("#L_GIAwayForWedding","GLOBAL",0)
	OR(2)
		!Exists("GARRICK")
		InPartyAllowDead("GARRICK")
THEN
	RESPONSE #100
		SetGlobal("#L_GarrickFeldepost","GLOBAL",0)
		SetGlobal("#L_GarrickPositioned","MYAREA",0)
		SetGlobal("#L_GIAtInn","GLOBAL",0)		// Continuous number of days both are at an inn (either one)
END

IF
	Global("#L_ImoenFeldepost","GLOBAL",1)
	Global("#L_GIAwayForWedding","GLOBAL",0)
	OR(2)
		!Exists("%IMOEN_DV%")
		InPartyAllowDead("%IMOEN_DV%")
THEN
	RESPONSE #100
		SetGlobal("#L_ImoenFeldepost","GLOBAL",0)
		SetGlobal("#L_ImoenPositioned","MYAREA",0)
		SetGlobal("#L_GIAtInn","GLOBAL",0)		// Continuous number of days both are at an inn (either one)
END

IF
	Global("#L_GarrickFeldepost","GLOBAL",1)
	Global("#L_GarrickPositioned","MYAREA",0)
THEN
	RESPONSE #100
		ActionOverride("GARRICK",Face(SW))
		SetGlobal("#L_GarrickPositioned","MYAREA",1)
END

IF
	Global("#L_ImoenFeldepost","GLOBAL",1)
	Global("#L_ImoenPositioned","MYAREA",0)
THEN
	RESPONSE #100
		ActionOverride("%IMOEN_DV%",Face(NE))
		SetGlobal("#L_ImoenPositioned","MYAREA",1)
END

IF
	Global("#L_GarrickFeldepost","GLOBAL",1)
	OR(2)
		Global("#L_GIPerformTimer","MYAREA",0)
		GlobalTimerExpired("#L_GIPerformTimer","MYAREA")
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobalTimer("#L_GIPerformTimer","MYAREA",ONE_DAY)	// He performs at most once per day
		StartCutSceneEx("#LGISing",TRUE)
END

IF
	OR(2)
		Global("#L_ImoenFeldepost","GLOBAL",1)
		Global("#L_GarrickFeldepost","GLOBAL",1)
THEN
	RESPONSE #100
		SetGlobal("#L_GIPineTimer","GLOBAL",0)
		Continue()
END

// The patrons bemoan Garrick leaving
// For the next three visits to the inn...not forever
IF
	Global("#L_GarrickTOK_BG1","GLOBAL",1)
	Global("#L_PatronChatter","MYAREA",0)
	GlobalGT("#L_GIAtFeldepost",GLOBAL",5)	// If he was at Feldeposts for longer than 5 days
	OR(2)
		Global("#L_GIPatronChatterTimer","MYAREA",0)
		GlobalTimerExpired("#L_GIPatronChatterTimer","MYAREA")
THEN
	RESPONSE #100
		SetGlobal("#L_PatronChatter","MYAREA",1)
		SetGlobalTimer("#L_GIPatronChatterTimer","MYAREA",ONE_DAY)
		StartCutSceneMode()
		DisplayStringHead("Patron_1",@2653) // ~It's just not the same without Garrick here.~
		Wait(2)
		EndCutSceneMode()
		Continue()
END

IF
	Global("#L_GarrickTOK_BG1","GLOBAL",1)
	Global("#L_PatronChatter","MYAREA",1)
	GlobalTimerExpired("#L_GIPatronChatterTimer","MYAREA")
THEN
	RESPONSE #100
		SetGlobal("#L_PatronChatter","MYAREA",2)
		SetGlobalTimer("#L_GIPatronChatterTimer","MYAREA",ONE_DAY)
		StartCutSceneMode()
		DisplayStringHead("Patron_2",@2654) // ~I wish Garrick the best of luck up in Baldur's Gate.~
		Wait(2)
		EndCutSceneMode()
		Continue()
END

IF
	Global("#L_GarrickTOK_BG1","GLOBAL",1)
	Global("#L_PatronChatter","MYAREA",2)
	GlobalTimerExpired("#L_GIPatronChatterTimer","MYAREA")
THEN
	RESPONSE #100
		SetGlobal("#L_PatronChatter","MYAREA",3)
		SetGlobal("#L_GIPatronChatterTimer","MYAREA",0)
		StartCutSceneMode()
		DisplayStringHead("Bartender",@2655) // ~Three Old Kegs better treat Garrick well or they'll hear from me!~
		Wait(2)
		EndCutSceneMode()
		Continue()
END
