////////////////////////////////////////////////
// Interrupts the beginning of the rest cycle //
// to do a quick scene                        //
////////////////////////////////////////////////

IF
	GlobalGT("#L_GIRomance","GLOBAL",0)
	GlobalLT("#L_GIRomance","GLOBAL",150)
	OR(2)
		Global("#L_GIRestTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIRestTimer","GLOBAL")
	IsValidForPartyDialogue("GARRICK")
	IsValidForPartyDialogue("%IMOEN_DV%")
	%TRIGGER_RESTING_INN%
THEN
	RESPONSE #100
		StartCutSceneMode()
		StartCutSceneEx("#LGIInn",TRUE)
END

IF
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue("%IMOEN_DV%")
	!AreaCheck("%Candlekeep_Library_L5%")
	OR(2)
		Global("#L_GIPineCampTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIPineCampTimer","GLOBAL")
	OR(3)
		AreaType(OUTDOOR)
		AreaType(DUNGEON)
		AreaType(FOREST)
THEN
	RESPONSE #100
		StartCutSceneMode()
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead("%IMOEN_DV%",@2405) // ~Imoen stares forlornly into the fire and sighs.~
		SetGlobalTimer("#L_GIPineCampTimer","GLOBAL",TEN_DAYS)
		Wait(5)
		EndCutSceneMode()
		RestParty()
END

IF
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue("GARRICK")
	!AreaCheck("%Candlekeep_Library_L5%")
	OR(2)
		Global("#L_GIPineCampTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIPineCampTimer","GLOBAL")
	OR(3)
		AreaType(OUTDOOR)
		AreaType(DUNGEON)
		AreaType(FOREST)
THEN
	RESPONSE #100
		StartCutSceneMode()
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead("GARRICK",@2406) // ~Garrick stares forlornly into the fire and sighs.~
		SetGlobalTimer("#L_GIPineCampTimer","GLOBAL",TEN_DAYS)
		Wait(5)
		EndCutSceneMode()
		RestParty()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",0)
	GlobalLT("#L_GIRomance","GLOBAL",150)
	!AreaCheck("%Candlekeep_Library_L5%")
	OR(2)
		Global("#L_GIRestTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIRestTimer","GLOBAL")
	IsValidForPartyDialogue("GARRICK")
	IsValidForPartyDialogue("%IMOEN_DV%")
	AreaType(DUNGEON)
THEN
	RESPONSE #100
		StartCutSceneMode()
		StartCutSceneEx("#LGIDngn",TRUE)
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",0)
	GlobalLT("#L_GIRomance","GLOBAL",150)
	!AreaCheck("%Candlekeep_Library_L5%")
	OR(2)
		Global("#L_GIRestTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIRestTimer","GLOBAL")
	IsValidForPartyDialogue("GARRICK")
	IsValidForPartyDialogue("%IMOEN_DV%")
	OR(2)
		AreaType(OUTDOOR)
		AreaType(FOREST)
THEN
	RESPONSE #100
		StartCutSceneMode()
		StartCutSceneEx("#LGICamp",TRUE)
END

