////////////////////////////////
// "idle" cut scene kick-offs //
////////////////////////////////

// After romance has started, every few in-game days they're not together the one in the party will comment
IF
	Range("%IMOEN_DV%",60)
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GIPineTimer","GLOBAL",0)
		Continue()
END

IF
	Global("#L_GIPineCounter","GLOBAL",0)
	GlobalGT("#L_GIRomance","GLOBAL",5)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("%IMOEN_DV%")
	!Range("%IMOEN_DV%",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",THREE_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",0)
	GlobalLT("#L_GIPineCounter","GLOBAL",6)
	GlobalGT("#L_GIRomance","GLOBAL",5)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("%IMOEN_DV%")
	!Range("%IMOEN_DV%",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",FIVE_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",5)
	GlobalLT("#L_GIPineCounter","GLOBAL",11)
	GlobalGT("#L_GIRomance","GLOBAL",5)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("%IMOEN_DV%")
	!Range("%IMOEN_DV%",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",TEN_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",10)
	GlobalLT("#L_GIPineCounter","GLOBAL",21)
	GlobalGT("#L_GIRomance","GLOBAL",5)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("%IMOEN_DV%")
	!Range("%IMOEN_DV%",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",THIRTY_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
	GlobalTimerExpired("#L_GIPineTimer","GLOBAL")
	IsValidForPartyDialogue(MYSELF)
	ActionListEmpty()
	TriggerOverride(Player1,ActionListEmpty())
THEN
	RESPONSE #100
		SetGlobal("#L_GIPine","GLOBAL",1)
		IncrementGlobal("#L_GIPineCounter","GLOBAL",1)
		StartDialogueNoSet(Player1)
END

// Check to see if the other is hurt
IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue("%IMOEN_DV%")
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("%IMOEN_DV%",100)
	HPPercentGT("%IMOEN_DV%",89)
	TriggerOverride("%IMOEN_DV%",ActionListEmpty())
	CombatCounter(0)
	ActionListEmpty()
	OR(2)
		Global("#L_GIImoenBetter","GLOBAL",0)
		GlobalTimerExpired("#L_GIImoenBetter","GLOBAL")
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobalTimer("#L_GIImoenBetter","GLOBAL",ONE_DAY)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		MoveToObject("%IMOEN_DV%")
		DisplayStringHead(MYSELF,@2451) // ~Oh Imoen!  You're hurt!  Let me kiss it and make it better.~
		Wait(2)
		ApplySpellRES("#LGIKiss","%IMOEN_DV%")
		Wait(1)
		EndCutSceneMode()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("%IMOEN_DV%",90)
	HPPercentGT("%IMOEN_DV%",50)
	!CombatCounter(0)
	OR(2)
		Global("#L_GIHelpImoen1","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpImoen1","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpImoen1","GLOBAL",THREE_ROUNDS)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead(MYSELF,@2453)	// ~Imoen could use some healing over here!~
		PauseGame()
		Attack(LastAttackerOf("%IMOEN_DV%"))
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	!CombatCounter(0)
	!Dead("%IMOEN_DV%")
	OR(2)
		HPPercent("%IMOEN_DV%",50)
		HPPercentLT("%IMOEN_DV%",50)
	OR(2)
		Global("#L_GIHelpImoen2","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpImoen2","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpImoen2","GLOBAL",ONE_ROUND)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead(MYSELF,@2455) // ~Help!  Help!  Imoen!  Imoen needs help!~
		PauseGame()
		Attack(LastAttackerOf("%IMOEN_DV%"))
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	Dead("%IMOEN_DV%")
	!CombatCounter(0)
	Global("#L_GIDeadImoen1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GIDeadImoen1","GLOBAL",1)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead(MYSELF,@2457) // ~NO!  Not Imoen!  You monster!  I'll have you for this!~
		PauseGame()
		Attack(LastAttackerOf("%IMOEN_DV%"))
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("%IMOEN_DV%",90)
	!Dead("%IMOEN_DV%")
	CombatCounter(0)
	OR(2)
		Global("#L_GIHelpImoen1","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpImoen1","GLOBAL")
	OR(2)
		Global("#L_GIHelpImoen2","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpImoen2","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpImoen1","GLOBAL",THREE_ROUNDS)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead(MYSELF,@2453)	// ~Imoen could use some healing over here!~
		PauseGame()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	Dead("%IMOEN_DV%")
	CombatCounter(0)
	Global("#L_GIDeadImoen2","GLOBAL",0)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("#L_GIDeadImoen2","GLOBAL",1)
		MoveViewObject("%IMOEN_DV%",INSTANT)
		MoveToObject("%IMOEN_DV%")
		DisplayStringHead(MYSELF,@2459) // ~NO! No, this cannot be!  We have to get Imoen to a temple immediately!  Oh my poor Immy!~
		Wait(1)
		EndCutSceneMode()
END

IF
	OR(2)
		Global("#L_GIDeadImoen1","GLOBAL",1)
		Global("#L_GIDeadImoen2","GLOBAL",1)
	!Dead("%IMOEN_DV%")
THEN
	RESPONSE #100
		SetGlobal("#L_GIDeadImoen1","GLOBAL",0)
		SetGlobal("#L_GIDeadImoen2","GLOBAL",0)
END

// Check to see if we're ready for an "idle" scene
IF
	IsValidForPartyDialogue("%IMOEN_DV%")
	IsValidForPartyDialogue(MYSELF)
	GlobalGT("#L_GIRomance","GLOBAL",0)
	OR(2)
		Global("#L_GIActionTimer","GLOBAL",0)
		GlobalTimerExpired("#L_GIActionTimer","GLOBAL")
	TriggerOverride("%IMOEN_DV%",ActionListEmpty())
	ActionListEmpty()
	Global("#L_GIIdle","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIIdleTimer","GLOBAL",ONE_ROUND)
		SetGlobal("#L_GIIdle","GLOBAL",1)
		Continue()
END

// Check again
IF
	IsValidForPartyDialogue("%IMOEN_DV%")
	IsValidForPartyDialogue("GARRICK")
	TriggerOverride("%IMOEN_DV%",ActionListEmpty())
	ActionListEmpty()
	Global("#L_GIIdle","GLOBAL",1)
	GlobalTimerExpired("#L_GIIdleTimer","GLOBAL")
THEN
	RESPONSE #100
		StartCutSceneMode()
		StartCutSceneEx("#LGIIdle",TRUE)
END

// Check failed
IF
	Global("#L_GIIdle","GLOBAL",1)
	GlobalTimerExpired("#L_GIIdleTimer","GLOBAL")
	OR(4)
		!IsValidForPartyDialogue("%IMOEN_DV%")
		!IsValidForPartyDialogue("GARRICK")
		!TriggerOverride("%IMOEN_DV%",ActionListEmpty())
		!ActionListEmpty()
THEN
	RESPONSE #100
		SetGlobal("#L_GIIdle","GLOBAL",0) // Find another idle moment
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	OR(6)
		StateCheck("%IMOEN_DV%",STATE_SLEEPING)
		StateCheck("%IMOEN_DV%",STATE_PANIC)
		StateCheck("%IMOEN_DV%",STATE_STUNNED)
		StateCheck("%IMOEN_DV%",STATE_CHARMED)
		StateCheck("%IMOEN_DV%",STATE_FEEBLEMINDED)
		StateCheck("%IMOEN_DV%",STATE_CONFUSED)
THEN
	RESPONSE #100
		StartCutSceneMode()
		MoveViewObject("%IMOEN_DV%",INSTANT)
		DisplayStringHead(MYSELF,@2461) // ~Oh my poor Immy!  I'm coming, Imoen!~
		MoveToObject("%IMOEN_DV%")
		ApplySpellRES("#LGIKiss","%IMOEN_DV%")
		Wait(1)
		EndCutSceneMode()
		Continue()
END
