//////////////////////////////////////////////////////////
// Move Garrick to the first SoD area if Imoen is there //
//////////////////////////////////////////////////////////

IF
	Global("C#IM_ImoenComesBackSoD","GLOBAL",1)
	Global("#L_GarrickMoved","MYAREA",0)
	OR(3)
		TriggerOverride(Player1,See("IMOEN2"))
		TriggerOverride("BDPRIN",LOS(Player1,30))
		TriggerOverride("BDFIST21",LOS(Player1,30))
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("#L_GarrickMoved","MYAREA",2)
		ActionOverride(Player1,CreateVisualEffect("SPDIMNDR",[1310.3230]))
		Wait(2)
		MoveGlobal("bd1000","GARRICK",[1310.3230])
		ActionOverride("GARRICK",ChangeAIScript("#LGar15O",OVERRIDE))
		ActionOverride("GARRICK",ChangeAIScript("BDGARRCC",CLASS))
		ActionOverride("GARRICK",ChangeAIScript("",RACE))
		ActionOverride("GARRICK",ChangeAIScript("",GENERAL))
//		ActionOverride("GARRICK",ChangeAIScript("BDPARTY",DEFAULT)) Maybe after he's joined the party?
		ActionOverride("GARRICK",ChangeAIScript("",DEFAULT))
		ActionOverride("GARRICK",SetDialogue("BDGARRIC"))
		SmallWait(2)
		FaceObject(Player1)
		EndCutSceneMode()
		Continue()
END

IF
	Exists("IMOEN2")
	Global("#L_GarrickMoved","MYAREA",2)
	Global("#L_GarrickFaceImoen","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GarrickFaceImoen","MYAREA",1)
		SetGlobal("#L_FollowingImoen","MYAREA",1)
		FaceObject("IMOEN2")
		MoveToObjectFollow("IMOEN2")
		Continue()
END

IF
	InParty("IMOEN2")
	Global("#L_GarrickMoved","MYAREA",2)
	Global("#L_GarrickJoined","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GarrickJoined","MYAREA",1)
		SetGlobal("#L_FollowingImoen","MYAREA",0)
		ActionOverride("GARRICK",SetDialogue("BDGARRIJ"))
		ActionOverride("GARRICK",JoinParty())
		Continue()
END

