////////////////////////////////////////////////////////////
// Kicks off Garrick's performances and Imoen's reactions //
////////////////////////////////////////////////////////////

// Failsafe for when other mods mess with parking spots
IF
	Global("#L_GarrickTOK_SoD","GLOBAL",1)
	OR(2)
		!Exists("GARRICK")
		InPartyAllowDead("GARRICK")
THEN
	RESPONSE #100
		SetGlobal("#L_GarrickTOK_SoD","GLOBAL",0)
		SetGlobal("#L_GarrickPositioned","MYAREA",0)
END

IF
	Global("#L_GarrickTOK_SoD","GLOBAL",1)
	Global("#L_GarrickPositioned","MYAREA",0)
THEN
	RESPONSE #100
		ActionOverride("GARRICK",Face(SE))
		SetGlobal("BD_No_Boot_Dialog","MYAREA",1)
		ActionOverride("GARRICK",SetGlobal("BD_Joined","LOCALS",0))
	    ActionOverride("GARRICK",SetGlobal("BD_Retreat","locals",0))
		ActionOverride("GARRICK",SetDialog("#LGar15S"))
		ActionOverride("GARRICK",ChangeAIScript("",OVERRIDE))
		ActionOverride("GARRICK",ChangeAIScript("DEFAULT",CLASS))
		ActionOverride("GARRICK",ChangeAIScript("",RACE))
		ActionOverride("GARRICK",ChangeAIScript("",GENERAL))
		ActionOverride("GARRICK",ChangeAIScript("",DEFAULT))
		AddMapNote([360.140],%STRREF_GARRICK%)
		SetGlobal("#L_GarrickPositioned","MYAREA",1)
END

IF
	Global("#L_GarrickTOK_SoD","GLOBAL",1)
	OR(2)
		Global("#L_GIPerformTimer","MYAREA",0)
		GlobalTimerExpired("#L_GIPerformTimer","MYAREA")
	TriggerOverride("GARRICK",LOS(Player1,30))
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobalTimer("#L_GIPerformTimer","MYAREA",ONE_DAY)	// He performs at most once per day
		StartCutSceneEx("#LGISing",TRUE)
END

IF
	GlobalLT("bd_visited_three_npcs","global",3)
	Global("bd_stop_npc_increment","MYAREA",0)
	GlobalGT("BD_Spoken_Garrick","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("bd_stop_npc_increment","MYAREA",1)
		IncrementGlobal("bd_visited_three_npcs","global",1)
END

