//////////////////////////////////
// "pining" cut scene kick-offs //
//////////////////////////////////

// After romance has started, every few in-game days they're not together the one in the party will comment
IF
	Range("GARRICK",60)
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GIPineTimer","GLOBAL",0)
		Continue()
END

IF
	Global("#L_GIPineCounter","GLOBAL",0)
	GlobalGT("#L_GIRomance","GLOBAL",0)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("GARRICK")
	!Range("GARRICK",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",THREE_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",0)
	GlobalLT("#L_GIPineCounter","GLOBAL",6)
	GlobalGT("#L_GIRomance","GLOBAL",0)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("GARRICK")
	!Range("GARRICK",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",FIVE_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",5)
	GlobalLT("#L_GIPineCounter","GLOBAL",11)
	GlobalGT("#L_GIRomance","GLOBAL",0)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("GARRICK")
	!Range("GARRICK",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",TEN_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineCounter","GLOBAL",10)
	GlobalLT("#L_GIPineCounter","GLOBAL",21)
	GlobalGT("#L_GIRomance","GLOBAL",0)
	Global("#L_GIPineTimer","GLOBAL",0)
	IsValidForPartyDialogue(MYSELF)
	!InPartyAllowDead("GARRICK")
	!Range("GARRICK",60)
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIPineTimer","GLOBAL",THIRTY_DAYS)
		Continue()
END

IF
	GlobalGT("#L_GIPineTimer","GLOBAL",0)
	GlobalTimerExpired("#L_GIPineTimer","GLOBAL")
	IsValidForPartyDialogue(MYSELF)
	ActionListEmpty()
	TriggerOverride(Player1,ActionListEmpty())
THEN
	RESPONSE #100
		SetGlobal("#L_GIPine","GLOBAL",1)
		IncrementGlobal("#L_GIPineCounter","GLOBAL",1)
		StartDialogueNoSet(Player1)
END

// Check to see if the other is hurt
IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue("GARRICK")
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("GARRICK",100)
	HPPercentGT("GARRICK",89)
	TriggerOverride("GARRICK",ActionListEmpty())
	CombatCounter(0)
	ActionListEmpty()
	OR(2)
		Global("#L_GIGarrickBetter","GLOBAL",0)
		GlobalTimerExpired("#L_GIGarrickBetter","GLOBAL")
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobalTimer("#L_GIGarrickBetter","GLOBAL",ONE_DAY)
		MoveViewObject("GARRICK",INSTANT)
		MoveToObject("GARRICK")
		DisplayStringHead(MYSELF,@2450) // ~Oh Garrick!  You're hurt!  Let me kiss it and make it better.~
		Wait(2)
		ApplySpellRES("#LGIKiss","GARRICK")
		Wait(1)
		EndCutSceneMode()
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("GARRICK",90)
	HPPercentGT("GARRICK",50)
	!CombatCounter(0)
	OR(2)
		Global("#L_GIHelpGarrick1","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpGarrick1","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpGarrick1","GLOBAL",THREE_ROUNDS)
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead(MYSELF,@2452) // ~Garrick could use some healing over here!~
		PauseGame()
		Attack(LastAttackerOf("GARRICK"))
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	!CombatCounter(0)
	!Dead("GARRICK")
	OR(2)
		HPPercent("GARRICK",50)
		HPPercentLT("GARRICK",50)
	OR(2)
		Global("#L_GIHelpGarrick2","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpGarrick2","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpGarrick2","GLOBAL",ONE_ROUND)
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead(MYSELF,@2454) // ~Help!  Help!  Garrick!  Someone help Garrick!~
		PauseGame()
		Attack(LastAttackerOf("GARRICK"))
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	Dead("GARRICK")
	!CombatCounter(0)
	Global("#L_GIDeadGarrick1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_GIDeadGarrick1","GLOBAL",1)
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead(MYSELF,@2456) // ~NO!  Not Garrick!  You monster!  I'll have you for this!~
		PauseGame()
		Attack(LastAttackerOf("GARRICK"))
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	HPPercentLT("GARRICK",90)
	!Dead("GARRICK")
	CombatCounter(0)
	OR(2)
		Global("#L_GIHelpGarrick1","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpGarrick1","GLOBAL")
	OR(2)
		Global("#L_GIHelpGarrick2","GLOBAL",0)
		GlobalTimerExpired("#L_GIHelpGarrick2","GLOBAL")
THEN
	RESPONSE #100
		SetGlobalTimer("#L_GIHelpGarrick1","GLOBAL",THREE_ROUNDS)
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead(MYSELF,@2452) // ~Garrick could use some healing over here!~
		PauseGame()
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	Dead("GARRICK")
	CombatCounter(0)
	Global("#L_GIDeadGarrick2","GLOBAL",0)
THEN
	RESPONSE #100
		StartCutSceneMode()
		SetGlobal("#L_GIDeadGarrick2","GLOBAL",1)
		MoveViewObject("GARRICK",INSTANT)
		MoveToObject("GARRICK")
		DisplayStringHead(MYSELF,@2458) // ~NO! No, this cannot be!  We have to get Garrick to a temple immediately!  Oh my poor Garrick!~
		Wait(1)
		EndCutSceneMode()
		Continue()
END

IF
	OR(2)
		Global("#L_GIDeadGarrick1","GLOBAL",1)
		Global("#L_GIDeadGarrick2","GLOBAL",1)
	!Dead("GARRICK")
THEN
	RESPONSE #100
		SetGlobal("#L_GIDeadGarrick1","GLOBAL",0)
		SetGlobal("#L_GIDeadGarrick2","GLOBAL",0)
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	IsValidForPartyDialogue(MYSELF)
	OR(6)
		StateCheck("GARRICK",STATE_SLEEPING)
		StateCheck("GARRICK",STATE_PANIC)
		StateCheck("GARRICK",STATE_STUNNED)
		StateCheck("GARRICK",STATE_CHARMED)
		StateCheck("GARRICK",STATE_FEEBLEMINDED)
		StateCheck("GARRICK",STATE_CONFUSED)
THEN
	RESPONSE #100
		StartCutSceneMode()
		MoveViewObject("GARRICK",INSTANT)
		DisplayStringHead(MYSELF,@2460) // ~Oh my poor Garrick!  I'm coming, Garrick!~
		MoveToObject("GARRICK")
		ApplySpellRES("#LGIKiss","GARRICK")
		Wait(1)
		EndCutSceneMode()
		Continue()
END
