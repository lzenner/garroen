/////////////////////////////////////////////
// Cut scenes for when they are at the inn //
/////////////////////////////////////////////

IF
	GlobalLT("#L_GIRomance","GLOBAL",6)
	Global("SilkeFightOver","GLOBAL",0)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2017) // ~My next song is about the bitter pain of unrequited love.~
		Continue()
END

IF
	GlobalLT("#L_GIRomance","GLOBAL",6)
	Global("SilkeFightOver","GLOBAL",1)
	Dead("Silke")
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2010) // ~My next song is about brave <CHARNAME>'s defeat of the evil Silke.~
		Continue()
END

IF
	GlobalLT("#L_GIRomance","GLOBAL",6)
	Global("SilkeFightOver","GLOBAL",1)
	!Dead("Silke")
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2011) // ~My next song is about brave <CHARNAME>'s rescue of my former employer, Silke.~
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",5)
	GlobalLT("#L_GIRomance","GLOBAL",11)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2020) // ~Here's a song for a lovely girl I know.~
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",10)
	GlobalLT("#L_GIRomance","GLOBAL",21)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2030) // ~I just wrote this.  It's about the girl who holds my heart.~
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",20)
	GlobalLT("#L_GIRomance","GLOBAL",36)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2040) // ~Her kisses taste like the finest wine!  Oh how I love that girl of mine!~
		Continue()
END

IF
	GlobalGT("#L_GIRomance","GLOBAL",35)
	GlobalLT("#L_GIRomance","GLOBAL",51)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		DisplayStringHead("GARRICK",@2050) // ~This next song is for the girl who drives me crazy.  She stole my heart, that rogue of mine!~
		Continue()
END

IF
	Global("#L_ImoenFeldepost","GLOBAL",1)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		StartCutSceneEx("#LGISigh",TRUE)
END

IF
	OR(2)
		GlobalGT("#L_GIRomance","GLOBAL",50)
		!Global("#L_ImoenFeldepost","GLOBAL",1)
THEN
	RESPONSE #100
		CutSceneId(Player1)
		EndCutSceneMode()
END

