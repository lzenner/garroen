////////////////////////////////////////////////
// Globals, timers and movements between inns //
////////////////////////////////////////////////

// Set global letting other mods know this one is installed
IF
	Global("#L_Garroen","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("#L_Garroen","GLOBAL",1)
		Continue()
END

// Every in-game day the romance will advance 
// when the two are parked in the same spot
IF
	Global("#L_GarrickFeldepost","GLOBAL",1)
	Global("#L_ImoenFeldepost","GLOBAL",1)
	OR(2)
		Global("#L_GIParkedDay","GLOBAL",0)
		GlobalTimerExpired("#L_GIParkedDay","GLOBAL")
THEN
	RESPONSE #100
		IncrementGlobal("#L_GIRomance","GLOBAL",1)
		IncrementGlobal("#L_GIAtInn","GLOBAL",1)
		SetGlobalTimer("#L_GIParkedDay","GLOBAL",ONE_DAY)
		Continue()
END

// Every in-game day the romance will advance 
// when the Imoen is in traning and Garrick is at Three Old Kegs
IF
	Global("#L_ImoenInPalace","GLOBAL",1)
	OR(2)
		Global("#L_GarrickTOK_BG1","GLOBAL",1)
		Global("#L_GarrickTOK_SoD","GLOBAL",1)
	OR(2)
		Global("#L_GIParkedDay","GLOBAL",0)
		GlobalTimerExpired("#L_GIParkedDay","GLOBAL")
THEN
	RESPONSE #100
		IncrementGlobal("#L_GIRomance","GLOBAL",1)
		SetGlobalTimer("#L_GIParkedDay","GLOBAL",ONE_DAY)
		Continue()
END

// It can take a few rounds for the Garrick Infatuation variables to set themselves
// By this time, Imoen is already flirting with Garrick
// Have her back off until the infatuation gets to the 'your my best friend' part
IF
	InPartyAllowDead("%IMOEN_DV%")
	InPartyAllowDead("GARRICK")
	GlobalLT("#L_GIRomance","GLOBAL",5)
	GlobalLT("P#GarrickFlirt","GLOBAL",44)
THEN
	RESPONSE #100
		// Don't continue with the rest of the Garrick Flirt mod
		SetGlobal("#L_GIRomance","GLOBAL",0)
		SetGlobal("#L_GIPineTimer","GLOBAL",0)
		Continue()
END

IF
	// If they've spent more than 5 days together at an inn
	// Or we're no longer in BG1
	GlobalLT("P#GarrickFlirt","GLOBAL",99)
	Global("P#GarrickMatch","GLOBAL",1)
	OR(2)
		GlobalGT("#L_GIAtInn","GLOBAL",5)
		GlobalGT("EndOfBG1","GLOBAL",0)
THEN
	RESPONSE #100
		// Don't continue with the rest of the Garrick Flirt mod
		SetGlobal("P#GarrickFlirt","GLOBAL",99)
		Continue()
END

IF
	// If we've reached the end of the friendship portion of the infatuation
	// and Imoen is in the group
	InPartyAllowDead("%IMOEN_DV%")
	GlobalLT("P#GarrickFlirt","GLOBAL",99)
	GlobalGT("P#GarrickFlirt","GLOBAL",43)
THEN
	RESPONSE #100
		// Don't continue with the rest of the Garrick Flirt mod
		SetGlobal("P#GarrickFlirt","GLOBAL",99)
		Continue()
END

// Stop BG1NPC from skipping Garrick's going to Feldepost
IF
	Global("T1M2GarrLetter","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("T1M2GarrLetter","GLOBAL",1)
		Continue()
END

%MOVE_GARRICK_TOK_BG1%

%MOVE_GARRICK_TOK_SOD%

IF
	Global("#L_GIMarried","GLOBAL",0)
	Global("#L_ImoenInPalace","GLOBAL",0)
	Global("EndOfBG1","GLOBAL",0)
	GlobalTimerExpired("#L_GIAwayForWedding","GLOBAL")
THEN
	RESPONSE #100
		SetGlobal("#L_GIMarried","GLOBAL",1)
		SetGlobal("#L_GIRomance","GLOBAL",101)
		SetGlobal("#L_GarrickPositioned","%Beregost_FeldepostsInn_L1%",0)
		SetGlobal("#L_ImoenPositioned","%Beregost_FeldepostsInn_L1%",0)
		SetGlobal("#L_GarrickFeldepost","GLOBAL",1)
		SetGlobal("#L_ImoenFeldepost","GLOBAL",1)
		MoveGlobal("%Beregost_FeldepostsInn_L1%","GARRICK",[900.510])
		MoveGlobal("%Beregost_FeldepostsInn_L1%","%IMOEN_DV%",[780.580])
		Continue()
END

IF
	// If they're in the group together
	// and the flirt isn't going to continue
	// and the romance hasn't started yet
	InPartyAllowDead("%IMOEN_DV%")
	InPartyAllowDead("GARRICK")
	Global("#L_GIRomance","GLOBAL",0)
	OR(4)
		Global("P#GarrickMatch","GLOBAL",0)	// Not loaded or not matched
	    GlobalGT("P#GarrickStopFlirt","GLOBAL",9) // Flirt has been aborted
		Global("P#GarrickFlirt","GLOBAL",99) // Reached the end of friendship portion
		GlobalGT("EndOfBG1","GLOBAL",0) // We're no longer in BG1
THEN
	RESPONSE #100
		// Start the romance
		SetGlobal("#L_GIRomance","GLOBAL",1)
		SetGlobalTimer("#L_GIPartyDay","GLOBAL",ONE_DAY)
		Continue()
END

// Increment the romance every day they're in the group together
IF
	InPartyAllowDead("%IMOEN_DV%")
	InPartyAllowDead("GARRICK")
	GlobalGT("#L_GIRomance","GLOBAL",0)
	GlobalTimerExpired("#L_GIPartyDay","GLOBAL")
THEN
	RESPONSE #100
		IncrementGlobal("#L_GIRomance","GLOBAL",1)
		SetGlobalTimer("#L_GIPartyDay","GLOBAL",ONE_DAY)
		Continue()
END

